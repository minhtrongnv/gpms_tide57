const std = @import("std");
const engine = @import("engine");
const style = @import("style");
const sprite = @import("sprite");
const render = @import("render");
const chart = @import("chart");
const compose = @import("compose"); // the runtime compositor, for a whole baked library
const catalog_embed = @import("catalog"); // embedded portrayal assets (colour profile)
const common = @import("common.zig");
const Flags = common.Flags;
const usageErr = common.usageErr;

// TILE57_COLORPROFILE override via the portrayal C shim (mirrors the decl in
// src/chart.zig — env/IO live in C). NULL -> the embedded profile.
extern fn tg_colorprofile_override(len: *usize) callconv(.c) ?[*]const u8;
const resolveRulesDir = common.resolveRulesDir;

/// Every *.pmtiles under `dir`, recursively — the charts a baked library's
/// compositor is opened over.
fn archivePaths(io: std.Io, a: std.mem.Allocator, dir: []const u8) ![]const []const u8 {
    var out = std.ArrayList([]const u8).empty;
    var d = std.Io.Dir.cwd().openDir(io, dir, .{ .iterate = true }) catch return out.items;
    defer d.close(io);
    var walker = d.walk(a) catch return out.items;
    defer walker.deinit();
    while (walker.next(io) catch null) |entry| {
        if (entry.kind != .file or !std.mem.endsWith(u8, entry.path, ".pmtiles")) continue;
        try out.append(a, try std.fs.path.join(a, &.{ dir, entry.path }));
    }
    return out.items;
}

// tile57 png|pdf <cell.000 | bundle.pmtiles> <z> <x> <y> -o <out> [flags]  (one tile)
// tile57 png|pdf <source> --view <lon,lat,zoom> --size WxH -o <out> [flags] (a view)
// tile57 png|pdf <baked-library-dir> --view <lon,lat,zoom> -o <out> [flags]  (composed)
// The render-engine pixel path: parse + portray a cell (or replay a baked
// PMTiles bundle), drive the engine through PixelSurface -> RasterCanvas ->
// PNG, or the same op stream -> PdfCanvas -> a deterministic vector PDF with
// real text objects. A view renders ONE whole scene across every covering
// tile (labels + declutter over the full canvas, no seams).
/// The language a live render portrays in. The mariner's code wins when the
/// chart states it. Failing that an S-57 national name answers, because S-57
/// records no language for NOBJNM and the adapter tags it `und`. Written into
/// `buf` because the portrayal context takes a NUL-terminated string.
fn chartLanguage(a: std.mem.Allocator, adapted: []const engine.s101.adapter.Adapted, pref: []const u8, buf: *[16]u8) [:0]const u8 {
    const langs = engine.s101.adapter.languages(a, adapted) catch return "eng";
    var pick: []const u8 = "";
    for (langs) |l| {
        if (std.mem.eql(u8, l, pref)) pick = l;
    }
    if (pick.len == 0) {
        for (langs) |l| {
            if (std.mem.eql(u8, l, "und")) pick = l;
        }
    }
    if (pick.len == 0 or pick.len >= buf.len) return "eng";
    @memcpy(buf[0..pick.len], pick);
    buf[pick.len] = 0;
    return buf[0..pick.len :0];
}

/// Install a fallback face for scripts the bundled Noto Sans has no glyphs for,
/// from the path in TILE57_FONT_FALLBACK. The bytes are leaked deliberately:
/// render.font borrows them for the life of the process.
fn loadFallbackFont(io: std.Io, a: std.mem.Allocator) void {
    const p = std.c.getenv("TILE57_FONT_FALLBACK") orelse return;
    const bytes = std.Io.Dir.cwd().readFileAlloc(io, std.mem.span(p), a, .limited(128 << 20)) catch return;
    render.font.setFallback(bytes);
}

pub fn run(io: std.Io, a: std.mem.Allocator, args: []const [:0]const u8, output: render.pixel.Output) !void {
    if (args.len < 4) {
        std.debug.print("usage: tile57 {s} <cell.000|bundle.pmtiles> <z> <x> <y> -o <out> [--size N] [--palette day|dusk|night] [--rules DIR] [--dq] [--meta] [--scale F]\n" ++
            "       tile57 {s} <source> --view <lon,lat,zoom> --size WxH -o <out> [flags]\n", .{ @tagName(output), @tagName(output) });
        return;
    }
    const path = args[2];
    const tile_mode = args[3].len > 0 and args[3][0] != '-';
    var z: u8 = 0;
    var x: u32 = 0;
    var y: u32 = 0;
    if (tile_mode) {
        if (args.len < 6) return usageErr("tile mode needs z x y");
        z = std.fmt.parseInt(u8, args[3], 10) catch return usageErr("bad z");
        x = std.fmt.parseInt(u32, args[4], 10) catch return usageErr("bad x");
        y = std.fmt.parseInt(u32, args[5], 10) catch return usageErr("bad y");
    }

    var out_path: ?[]const u8 = null;
    var size_w: u32 = 256;
    var size_h: u32 = 256;
    var palette: render.resolve.PaletteId = .day;
    var rules: ?[]const u8 = null;
    var dq = false;
    var size_scale: f64 = 1.0; // physical-size multiplier (S-52 mm -> true mm)
    var view: ?struct { lon: f64, lat: f64, zoom: f64 } = null;
    // Mariner settings (defaults match the app: other ON for spot soundings).
    var m = render.resolve.Settings{ .display_other = true };
    var f = Flags{ .args = args, .i = if (tile_mode) 5 else 2 };
    while (f.next()) |arg| {
        if (std.mem.eql(u8, arg, "-o")) {
            out_path = f.next() orelse return usageErr("-o needs a path");
        } else if (std.mem.eql(u8, arg, "--size")) {
            const v = f.next() orelse return usageErr("--size needs a value");
            if (std.mem.indexOfScalar(u8, v, 'x')) |xi| {
                size_w = std.fmt.parseInt(u32, v[0..xi], 10) catch return usageErr("bad --size");
                size_h = std.fmt.parseInt(u32, v[xi + 1 ..], 10) catch return usageErr("bad --size");
            } else {
                size_w = std.fmt.parseInt(u32, v, 10) catch return usageErr("bad --size");
                size_h = size_w;
            }
        } else if (std.mem.eql(u8, arg, "--view")) {
            const v = f.next() orelse return usageErr("--view needs lon,lat,zoom");
            var it = std.mem.splitScalar(u8, v, ',');
            const lon = std.fmt.parseFloat(f64, it.next() orelse "") catch return usageErr("bad --view lon");
            const lat = std.fmt.parseFloat(f64, it.next() orelse "") catch return usageErr("bad --view lat");
            const zm = std.fmt.parseFloat(f64, it.next() orelse "") catch return usageErr("bad --view zoom");
            view = .{ .lon = lon, .lat = lat, .zoom = zm };
        } else if (std.mem.eql(u8, arg, "--palette")) {
            const v = f.next() orelse return usageErr("--palette needs a value");
            palette = std.meta.stringToEnum(render.resolve.PaletteId, v) orelse return usageErr("palette must be day|dusk|night");
        } else if (std.mem.eql(u8, arg, "--rules")) {
            rules = f.next() orelse return usageErr("--rules needs a dir");
        } else if (std.mem.eql(u8, arg, "--dq")) {
            dq = true; // S-52 data-quality overlay (M_QUAL DQUAL* patterns)
        } else if (std.mem.eql(u8, arg, "--meta")) {
            m.show_meta_bounds = true; // meta-object coverage/scale/nav-system bounds inspection view
        } else if (std.mem.eql(u8, arg, "--scale")) {
            const v = f.next() orelse return usageErr("--scale needs a value");
            size_scale = std.fmt.parseFloat(f64, v) catch return usageErr("bad --scale");
        } else if (std.mem.eql(u8, arg, "--safety")) {
            const v = f.next() orelse return usageErr("--safety needs metres");
            m.safety_contour = std.fmt.parseFloat(f64, v) catch return usageErr("bad --safety");
        } else if (std.mem.eql(u8, arg, "--safety-depth")) {
            const v = f.next() orelse return usageErr("--safety-depth needs metres");
            m.safety_depth = std.fmt.parseFloat(f64, v) catch return usageErr("bad --safety-depth");
        } else if (std.mem.eql(u8, arg, "--shallow")) {
            const v = f.next() orelse return usageErr("--shallow needs metres");
            m.shallow_contour = std.fmt.parseFloat(f64, v) catch return usageErr("bad --shallow");
        } else if (std.mem.eql(u8, arg, "--deep")) {
            const v = f.next() orelse return usageErr("--deep needs metres");
            m.deep_contour = std.fmt.parseFloat(f64, v) catch return usageErr("bad --deep");
        } else if (std.mem.eql(u8, arg, "--feet")) {
            m.depth_unit = .feet;
        } else if (std.mem.eql(u8, arg, "--language")) {
            m.preferred_language = f.next() orelse return usageErr("--language needs an ISO 639-2 code");
        } else if (std.mem.eql(u8, arg, "--no-names")) {
            m.text_names = false;
        } else if (std.mem.eql(u8, arg, "--no-light-text")) {
            m.show_light_descriptions = false;
        } else if (std.mem.eql(u8, arg, "--no-other-text")) {
            m.text_other = false;
        } else if (std.mem.eql(u8, arg, "--no-other")) {
            m.display_other = false;
        } else if (std.mem.eql(u8, arg, "--over-image")) {
            // Chart over picture: drop the opaque water/land fills so a raster
            // chart beneath shows through. Renders on a transparent background
            // here, which is how you see what actually survives.
            m.chart_over_image = true;
        } else if (std.mem.eql(u8, arg, "--soundings")) {
            m.show_soundings = true; // the everyday setting: STANDARD, soundings on
        } else if (std.mem.eql(u8, arg, "--no-soundings")) {
            m.show_soundings = false;
        } else if (std.mem.eql(u8, arg, "--plain")) {
            m.boundary_style = .plain;
        } else if (std.mem.eql(u8, arg, "--simplified")) {
            m.simplified_points = true;
        } else if (std.mem.eql(u8, arg, "--full-sectors")) {
            m.show_full_sector_lines = true;
        } else return usageErr("unknown flag");
    }
    const out = out_path orelse return usageErr("-o <out.png> is required");
    if (!tile_mode and view == null) return usageErr("--view lon,lat,zoom is required without z x y");

    // Baked tiles are the only multi-cell path: an ENC_ROOT is baked first, then
    // rendered — either one chart's .pmtiles (tile replay) or, for a whole baked
    // library directory, the compositor's view over every chart in it.
    const is_dir = blk: {
        var d = std.Io.Dir.cwd().openDir(io, path, .{}) catch break :blk false;
        d.close(io);
        break :blk true;
    };
    if (is_dir) {
        const v = view orelse return usageErr("a baked library directory needs --view lon,lat,zoom");
        const paths = try archivePaths(io, a, path);
        if (paths.len == 0) {
            std.debug.print("no *.pmtiles under {s} — bake first:\n  tile57 bake {s} -o <out>\n  tile57 {s} <out> --view {d},{d},{d} -o {s}\n", .{ path, path, @tagName(output), v.lon, v.lat, v.zoom, out });
            return;
        }
        const src = (compose.ComposeSource.openFiles(io, a, paths, null) catch return usageErr("cannot open the baked library")) orelse
            return usageErr("no chart in the library carries coverage");
        defer src.deinit();
        m.data_quality = dq;
        m.size_scale = size_scale;
        const settings = m;
        const bytes = try chart.renderComposeView(src, v.lon, v.lat, v.zoom, size_w, size_h, palette, &settings, output, null);
        defer chart.freeBytes(bytes);
        try std.Io.Dir.cwd().writeFile(io, .{ .sub_path = out, .data = bytes });
        std.debug.print("{s}: {d} chart(s), {d}x{d} at {d},{d} z{d} -> {d} bytes\n", .{ out, paths.len, size_w, size_h, v.lon, v.lat, v.zoom, bytes.len });
        return;
    }

    const from_bundle = std.mem.endsWith(u8, path, ".pmtiles");
    if (from_bundle and view == null) return usageErr("a .pmtiles source needs --view");

    const data = try std.Io.Dir.cwd().readFileAlloc(io, path, a, .unlimited);
    var cell: engine.s57.Cell = undefined;
    var streams: []const ?[]const u8 = &.{};
    if (!from_bundle) {
        // Auto-apply the cell's sequential .001.. updates beside it (like the
        // streaming chart loader and `tile57 explore`) — a bare-.000 render of a
        // real NOAA cell without its updates shows stale/deleted features.
        engine.portray.setQuiet(true);
        loadFallbackFont(io, a);
        // LIVE portrayal context: the mariner's real safety contour / depth /
        // contours / styles evaluate INSIDE the rules — the native win over
        // the tile path's fixed bake context.
        const pctx = engine.portray.Context{
            .safety_contour = m.safety_contour,
            .safety_depth = m.safety_depth,
            .shallow_contour = m.shallow_contour,
            .deep_contour = m.deep_contour,
            .plain_boundaries = m.boundary_style == .plain,
            .simplified_symbols = m.simplified_points,
            .full_light_lines = m.show_full_sector_lines,
        };
        // A native S-101 dataset (.000, S-100 Part 10a) assembles + portrays without
        // the S-57 -> S-101 adapter; either format applies its .001.. update chain.
        // A live render portrays once, so the national name is selected by
        // portraying in that language rather than by baking a twin beside the
        // label. Passing the language the cell states leaves a cell whose names
        // are all English portraying as it did.
        var lctx = pctx;
        var lang_buf: [16]u8 = undefined;
        if (engine.s101.dataset.detect(data)) {
            const loaded = try engine.s101.native.parseDataset(a, data, readUpdates(io, a, path));
            cell = loaded.cell;
            if (m.preferred_language.len > 0) lctx.preferred_language = chartLanguage(a, loaded.adapted, m.preferred_language, &lang_buf);
            streams = try engine.portray.portrayCellWithAdapted(a, &cell, loaded.adapted, resolveRulesDir(rules), lctx);
        } else {
            cell = try engine.s57.parseCellWithUpdates(a, data, readUpdates(io, a, path));
            if (m.preferred_language.len > 0) {
                const ad = try engine.s101.adapter.adaptCell(a, &cell);
                lctx.preferred_language = chartLanguage(a, ad, m.preferred_language, &lang_buf);
                streams = try engine.portray.portrayCellWithAdapted(a, &cell, ad, resolveRulesDir(rules), lctx);
            } else {
                streams = try engine.portray.portrayCellWith(a, &cell, resolveRulesDir(rules), lctx);
            }
        }
    }
    defer if (!from_bundle) cell.deinit();

    // Honour the TILE57_COLORPROFILE override (same knob the viewer's render path
    // uses via chart.zig sharedColors) so `tile57 png` previews a custom profile.
    var ov_len: usize = 0;
    const profile_xml: []const u8 = if (tg_colorprofile_override(&ov_len)) |p|
        p[0..ov_len]
    else
        catalog_embed.colorprofile[0].bytes;
    var colors = try render.resolve.Colors.init(a, profile_xml);
    m.data_quality = dq;
    m.size_scale = size_scale;
    const settings = m;

    const zoom: f64 = if (view) |v| v.zoom else @floatFromInt(z);
    var ps = if (view != null) blk: {
        // 512-and-up outputs read as @2x (the CSS baseline is 256/tile).
        const dpr: f32 = if (@min(size_w, size_h) >= 512) 2 else 1;
        const zi = @round(zoom);
        const pt = 256.0 * std.math.pow(f64, 2.0, zoom - zi) * dpr;
        break :blk render.pixel.PixelSurface.initView(a, &colors, palette, &settings, zoom, size_w, size_h, @floatCast(pt), engine.tile.EXTENT);
    } else render.pixel.PixelSurface.init(a, &colors, palette, &settings, zoom, size_w, engine.tile.EXTENT);

    // Vector symbol store over the embedded catalogue, palette-matched CSS.
    const css_name = switch (palette) {
        .day => "daySvgStyle",
        .dusk => "duskSvgStyle",
        .night => "nightSvgStyle",
    };
    var css_data: []const u8 = "";
    for (catalog_embed.css) |e| {
        if (std.mem.eql(u8, e.name, css_name)) css_data = e.bytes;
    }
    const sym_srcs = try a.alloc(sprite.SvgSrc, catalog_embed.symbols.len);
    for (catalog_embed.symbols, 0..) |e, si| sym_srcs[si] = .{ .id = e.name, .svg = e.bytes };
    const fill_srcs = try a.alloc(sprite.AreaFillSrc, catalog_embed.areafills.len);
    for (catalog_embed.areafills, 0..) |e, fi| fill_srcs[fi] = .{ .id = e.name, .xml = e.bytes };
    const store = try sprite.CatalogStore.init(a, sym_srcs, fill_srcs, css_data);
    defer store.deinit();
    ps.store = store.asStore();
    ps.output = output;

    // Complex-linestyle table (idempotent; arena-backed — this run only).
    const ls_srcs = try a.alloc(style.LineStyleSrc, catalog_embed.linestyles.len);
    for (catalog_embed.linestyles, 0..) |e, li| ls_srcs[li] = .{ .id = e.name, .xml = e.bytes };
    engine.scene.linestyle.registerLinestylesXml(a, ls_srcs);

    const bytes = if (from_bundle) blk: {
        // Bundle-sourced replay: decode each covering baked tile and re-emit
        // it as Surface calls (bake context frozen; live-swappable props —
        // danger depth, sounding composition/unit — re-evaluate here).
        const v = view.?;
        var rd = try engine.pmtiles.Reader.init(a, data);
        defer rd.deinit();
        var vt = engine.scene.ViewTiles.init(v.lon, v.lat, v.zoom, size_w, size_h, ps.px_per_tile);
        const surf = ps.asSurface();
        try surf.beginScene(vt.z);
        const is_mlt = rd.header.tile_type == .mlt;
        while (vt.next()) |t| {
            const tb = (rd.getTile(a, t.z, t.x, t.y) catch continue) orelse continue;
            const layers = if (is_mlt)
                engine.mlt.decode(a, tb) catch continue
            else
                engine.mvt.decode(a, tb) catch continue;
            ps.setOrigin(t.origin_x, t.origin_y);
            try engine.scene.replayTile(a, surf, layers);
        }
        break :blk try surf.endScene(a);
    } else blk: {
        const cells = [_]engine.scene.CellRef{.{ .cell = &cell, .portrayal = streams }};
        break :blk if (view) |v|
            try engine.scene.generateView(&ps, a, a, &cells, v.lon, v.lat, v.zoom, false)
        else
            try engine.scene.generateTile(ps.asSurface(), a, a, &cells, z, x, y, false);
    };
    try std.Io.Dir.cwd().writeFile(io, .{ .sub_path = out, .data = bytes });
    if (view) |v| {
        std.debug.print("wrote {s}: view {d:.4},{d:.4} z{d:.2}, {d}x{d}px, {d} draw ops, {d} bytes\n", .{ out, v.lon, v.lat, v.zoom, size_w, size_h, ps.ops.items.len, bytes.len });
    } else {
        std.debug.print("wrote {s}: tile {d}/{d}/{d}, {d}x{d}px, {d} draw ops, {d} bytes\n", .{ out, z, x, y, size_w, size_h, ps.ops.items.len, bytes.len });
    }
}

// Auto-discover a base cell's sequential .001.. update files beside it
// (mirrors explore.exReadUpdates + the streaming chart loader). Missing file =
// end of chain; a non-.000 source has no updates.
fn readUpdates(io: std.Io, a: std.mem.Allocator, base_path: []const u8) []const []const u8 {
    if (!std.mem.endsWith(u8, base_path, ".000")) return &.{};
    const stem = base_path[0 .. base_path.len - 4];
    var list = std.ArrayList([]const u8).empty;
    var u: u32 = 1;
    while (u <= 999) : (u += 1) {
        const upn = std.fmt.allocPrint(a, "{s}.{d:0>3}", .{ stem, u }) catch break;
        const ub = std.Io.Dir.cwd().readFileAlloc(io, upn, a, .unlimited) catch break;
        list.append(a, ub) catch break;
    }
    return list.items;
}
